#pragma only_renderers d3d11 ps4 xboxone vulkan metal switch

#pragma kernel STAR main=STAR
#pragma kernel NEBULAE main=NEBULAE

#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "../../common/shaders/ExpanseNoise.hlsl"
#include "../../sky/ExpanseStarCommon.hlsl"

/* RW Textures. */
RW_TEXTURE2D_ARRAY(float4, _Star_RW);
RW_TEXTURE2D_ARRAY(float4, _Nebulae_RW);

[numthreads(8,8,6)]
void STAR(uint3 id : SV_DispatchThreadID) {
  /* Get the uv coordinate. */
  float2 uv = (id.xy + 0.5) / _resStar.xy;
  float3 uvw = float3(uv.x, uv.y, id.z);

  /* Transform it to a normalized direction vector. */
  float3 d = tex2DArrayCubemapUVToDirection(uvw);

  /* Convert the direction vector to a uv coordinate. */
  float3 textureCoordinate = (d + 1) / 2;

  /* Sample some noise. */
  float bigCells = 768;
  float smallCells = 2048;
  NoiseResultAndCoordinate noiseBig = worley3D(textureCoordinate,
    float3(bigCells, bigCells, bigCells));
  NoiseResultAndCoordinate noiseSmall = worley3D(textureCoordinate,
    float3(smallCells, smallCells, smallCells));

  /* Cache our seed value for flickering, from the big cell. */
  float starSeed = random_3_1(noiseBig.coordinate);

  /* Sample the procedural nebula to modify star density. */
  float nebulaeDensityModifier = 1.0;
  if (_useProceduralNebulae) {
    float3 nebulaeColor = SAMPLE_TEXTURE2D_ARRAY_LOD(_proceduralNebulae,
      s_linear_clamp_sampler, uv, id.z, 0).xyz;
      nebulaeDensityModifier += _starNebulaFollowAmount * pow(abs(dot(saturate(nebulaeColor), saturate(nebulaeColor))/3), _starNebulaFollowSpread);
  } else if (_hasNebulaeTexture) {
    float3 nebulaeColor = SAMPLE_TEXTURECUBE_LOD(_nebulaeTexture,
      s_linear_clamp_sampler, d, 0).xyz;
      nebulaeDensityModifier += _starNebulaFollowAmount * pow(abs(dot(saturate(nebulaeColor), saturate(nebulaeColor))/3), _starNebulaFollowSpread);
  }

  /*****************************************************/
  /******* Compute everything for the big stars. *******/
  /*****************************************************/
  /* Do we render this star? */
  float densityRandomBig = random_3_1_seeded(noiseBig.coordinate,
    _starDensitySeed.xyz) * nebulaeDensityModifier;
  bool renderBig = densityRandomBig > (1 - _starDensity) ? true : false;
  float3 starBigColor = float3(0, 0, 0);
  if (renderBig) {
    /* Transform the worley noise to look more like stars. */
    float sizeRandom = random_3_1_seeded(noiseBig.coordinate, _starSizeSeed.xyz);
    /* HACK: zero values screw up our biasing and result in artifacts. */
    float3 stars = float3(0, 0, 0);
    if (sizeRandom > 0.1) {
      sizeRandom = saturate(bias0To1(saturate(sizeRandom), _starSizeBias, 500));
      float sizeMinExponent = 5 + pow(abs(1 - _starSizeMax), 1) * 100;
      float sizeMaxExponent = 5 + pow(abs(1 - _starSizeMin), 1) * 100;
      stars = pow(abs(noiseBig.result), sizeMinExponent + max(0, sizeMaxExponent - sizeMinExponent) * sizeRandom);
    }

    /* Random intensity multiplier based on seed. */
    float intensityRandom = 1-bias0To1(random_3_1_seeded(noiseBig.coordinate, _starIntensitySeed.xyz), _starIntensityBias, 100);
    float intensity = _starIntensityMin + (_starIntensityMax - _starIntensityMin) * intensityRandom;

    /* Blackbody color based on seed. */
    float temperatureRandom = 1-bias0To1(random_3_1_seeded(noiseBig.coordinate, _starTemperatureSeed.xyz), _starTemperatureBias, 100);
    float randomTemperature = _starTemperatureMin + (_starTemperatureMax - _starTemperatureMin) * temperatureRandom;
    float3 blackBodyColor = blackbodyTempToColor(randomTemperature);

    starBigColor = intensity * blackBodyColor * stars;
  }

  /*******************************************************/
  /******* Compute everything for the small stars. *******/
  /*******************************************************/
  float3 starSmallColor = float3(0, 0, 0);
  if (_useHighDensityMode) {
    float densityRandomSmall = random_3_1_seeded(noiseSmall.coordinate,
      _starDensitySeed.xyz) * nebulaeDensityModifier;
    /* Do we render this star? */
    bool renderSmall = densityRandomSmall > (1 - _starDensity) ? true : false;
    if (renderSmall) {
      /* Transform the worley noise to look more like stars. */
      float sizeRandom = random_3_1_seeded(noiseSmall.coordinate, _starSizeSeed.xyz);
      /* HACK: zero values screw up our biasing and result in artifacts. */
      float3 stars = float3(0, 0, 0);
      if (sizeRandom != 0) {
        sizeRandom = saturate(bias0To1(saturate(sizeRandom), _starSizeBias, 500));
        float sizeMinExponent = 5 + pow(abs(1 - _starSizeMax), 1) * 100;
        float sizeMaxExponent = 5 + pow(abs(1 - _starSizeMin), 1) * 100;
        stars = pow(abs(noiseSmall.result), sizeMinExponent + (sizeMaxExponent - sizeMinExponent) * sizeRandom);
      }

      /* Random intensity multiplier based on seed. */
      float intensityRandom = 1-bias0To1(random_3_1_seeded(noiseSmall.coordinate, _starIntensitySeed.xyz), _starIntensityBias, 100);
      float intensity = _starIntensityMin + (_starIntensityMax - _starIntensityMin) * intensityRandom;

      /* Blackbody color based on seed. */
      float temperatureRandom = 1-bias0To1(random_3_1_seeded(noiseSmall.coordinate, _starTemperatureSeed.xyz), _starTemperatureBias, 100);
      float randomTemperature = _starTemperatureMin + (_starTemperatureMax - _starTemperatureMin) * temperatureRandom;
      float3 blackBodyColor = blackbodyTempToColor(randomTemperature);

      starSmallColor = intensity * blackBodyColor * stars;
    }
  }

  _Star_RW[id] = float4(starBigColor + starSmallColor, starSeed);
}

[numthreads(8,8,1)]
void NEBULAE(uint3 id : SV_DispatchThreadID) {
  /* Get the uv coordinate. */
  float2 uv = (id.xy + 0.5) / _resNebulae.xy;
  float3 uvw = float3(uv.x, uv.y, id.z);

  /* Transform it to a normalized direction vector. */
  float3 d = tex2DArrayCubemapUVToDirection(uvw);

  /* Convert the direction vector to a uv coordinate. */
  float3 textureCoordinate = (d + 1) / 2;

  /* Our nebula model requires 7 layers of noise.
   *  1. A layered perlin noise to create a hazey haze.
   *  2. A layered worley noise to create a cloudy look.
   *  3. A layered voronoi noise raised to a power, to create the sort
   *  of strandey shapes often visible in nebulae.
   *  4. A layered voronoi noise that serves the same function, but is
   *  higher frequency to capture more intricate details.
   *  5. A curl noise to warp the strandey voronoi noises to give the
   *  appearance of fluid motion.
   *  6. A value noise used to remap the other noises for sky coverage.
   *  7. A layered perlin noise to modulate the nebula alpha value to give
   *  the impression of varying star transmittance. */

  /* 1. Layered perlin noise for hazey base. */
  float haze = saturate(perlin3DLayeredSeeded(saturate(textureCoordinate),
    float3(_nebulaHazeScale, _nebulaHazeScale, _nebulaHazeScale), _nebulaHazeScaleFactor, _nebulaHazeDetailBalance,
    _nebulaHazeOctaves, _nebulaHazeSeedX.xyz, _nebulaHazeSeedY.xyz, _nebulaHazeSeedZ.xyz) + _nebulaHazeBias);

  /* 2. Layered worley noise for cloudy look. */
  float clouds = saturate(worley3DLayeredSeeded(saturate(textureCoordinate),
  float3(_nebulaCloudScale, _nebulaCloudScale, _nebulaCloudScale), _nebulaCloudScaleFactor,
  _nebulaCloudDetailBalance, _nebulaCloudOctaves, _nebulaCloudSeedX.xyz, _nebulaCloudSeedY.xyz,
  _nebulaCloudSeedZ.xyz) + _nebulaCloudBias);

  /* 5. Curl noise to warp strands. Have to compute first to advect uv
   * coordinate. */
  const float WARP_MAX = 0.08;
  float3 coarseCurl = curlNoise3DSeeded(saturate(textureCoordinate),
    float3(_nebulaCoarseStrandWarpScale, _nebulaCoarseStrandWarpScale, _nebulaCoarseStrandWarpScale),
    _nebulaCoarseStrandWarpSeedX.xyz, _nebulaCoarseStrandWarpSeedY.xyz, _nebulaCoarseStrandWarpSeedZ.xyz);
  float3 fineCurl = curlNoise3DSeeded(saturate(textureCoordinate),
    float3(_nebulaFineStrandWarpScale, _nebulaFineStrandWarpScale, _nebulaFineStrandWarpScale),
      _nebulaFineStrandWarpSeedX.xyz, _nebulaFineStrandWarpSeedY.xyz, _nebulaFineStrandWarpSeedZ.xyz);

  /* HACK: to avoid artifacts we get from warping at the poles, scale back the
   * warping effect as we approach a pole. */
  float dotx = abs(dot(d, float3(1, 0, 0)));
  float doty = abs(dot(d, float3(0, 1, 0)));
  float dotz = abs(dot(d, float3(0, 0, 1)));
  float distFromPole = min(min(safeSqrt(dotz * dotz + dotx * dotx),
    safeSqrt(dotx * dotx + doty * doty)),
    safeSqrt(dotz * dotz + doty * doty));
  if (distFromPole < 0.1) {
    coarseCurl *= (pow(abs(saturate(distFromPole) / (0.1)), 4));
    fineCurl *= (pow(abs(saturate(distFromPole) / (0.1)), 4));
  }

  /* 3. Layered voronoi noise to create coarse strands. */
  float3 warpedCoarseStrandTextureCoordinate = (normalize(d - _nebulaCoarseStrandWarp * WARP_MAX * coarseCurl) + 1) / 2;
  float coarseStrands = saturate(voronoi3DLayeredSeeded(saturate(warpedCoarseStrandTextureCoordinate),
    float3(_nebulaCoarseStrandScale, _nebulaCoarseStrandScale, _nebulaCoarseStrandScale),
    _nebulaCoarseStrandScaleFactor, _nebulaCoarseStrandDetailBalance, _nebulaCoarseStrandOctaves,
    _nebulaCoarseStrandSeedX.xyz, _nebulaCoarseStrandSeedY.xyz, _nebulaCoarseStrandSeedZ.xyz)
    + _nebulaCoarseStrandBias);

  /* 4. Layered voronoi noise to create fine strands. */
  float3 warpedFineStrandTextureCoordinate = (normalize(d - _nebulaFineStrandWarp * WARP_MAX * fineCurl) + 1) / 2;
  float fineStrands = saturate(voronoi3DLayeredSeeded(saturate(warpedFineStrandTextureCoordinate),
    float3(_nebulaFineStrandScale, _nebulaFineStrandScale, _nebulaFineStrandScale), _nebulaFineStrandScaleFactor,
    _nebulaFineStrandDetailBalance, _nebulaFineStrandOctaves, _nebulaFineStrandSeedX.xyz,
    _nebulaFineStrandSeedY.xyz, _nebulaFineStrandSeedZ.xyz) + _nebulaFineStrandBias);


  /* 6. Value noise for coverage. */
  float coverage = saturate(value3DSeeded(saturate(textureCoordinate),
    float3(_nebulaCoverageScale, _nebulaCoverageScale, _nebulaCoverageScale), _nebulaCoverageSeed.xyz).result);

  /* 7. Layered perlin noise for transmittance. Scaled to be between specified
   * min and max values. */
  float transmittance = perlin3DLayeredSeeded(saturate(textureCoordinate),
  float3(_nebulaTransmittanceScale, _nebulaTransmittanceScale, _nebulaTransmittanceScale),
    2, 0.5, 3, _nebulaTransmittanceSeedX.xyz, _nebulaTransmittanceSeedY.xyz, _nebulaTransmittanceSeedZ.xyz);
  transmittance = _nebulaTransmittanceMin
    + (_nebulaTransmittanceMax - _nebulaTransmittanceMin) * transmittance;

  /* Remap all the base noises using the coverage noise. */
  haze = saturate(remap(haze * _nebulaHazeStrength, pow(abs(coverage), _nebulaHazeSpread) * _nebulaHazeCoverage, 1, 0, 1));
  clouds = saturate(remap(clouds * _nebulaCloudStrength, pow(abs(coverage), _nebulaCloudSpread) * _nebulaCloudCoverage, 1, 0, 1));
  coarseStrands = saturate(remap(pow(abs(coarseStrands), _nebulaCoarseStrandDefinition) * _nebulaCoarseStrandStrength / pow(0.15, _nebulaCoarseStrandDefinition), pow(abs(coverage), _nebulaCoarseStrandSpread) * _nebulaCoarseStrandCoverage, 1, 0, 1));
  fineStrands = saturate(remap(pow(abs(fineStrands), _nebulaFineStrandDefinition) * _nebulaFineStrandStrength / pow(0.15, _nebulaFineStrandDefinition), pow(abs(coverage), _nebulaFineStrandSpread) * _nebulaFineStrandCoverage, 1, 0, 1));

  float3 finalColor = _nebulaHazeColor.xyz * haze + _nebulaCloudColor.xyz * clouds
    + _nebulaCoarseStrandColor.xyz * coarseStrands + _nebulaFineStrandColor.xyz * fineStrands;

  _Nebulae_RW[id] = float4(finalColor, transmittance);

}
